COPY ~%MOD_FOLDER%/armored_casting~ ~override~

COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ //copies all item files
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
    READ_BYTE    0x1F bard //reads the byte containing the fighter usability flag
    PATCH_IF ((bard BAND 0b01000000) = 0b00000000) BEGIN // if it is usable by bards
      READ_SHORT    0x1C type //reads the byte containing item type
	  READ_SHORT    0x22 appearance //reads the byte containing item appearance
      PATCH_IF (type = 2) AND (appearance = 16690) BEGIN // if it is leather armor
		LPF ALTER_EFFECT
			INT_VAR
				silent = 1
				match_opcode = 145
				opcode = 177
				target = 1
				parameter1 = 202
				parameter2 = 5
				timing = 2
			STR_VAR
				resource = C0ARCAS1
		END
      END
      PATCH_IF (type = 2) AND (appearance = 16691) OR (appearance = 16692) BEGIN // if it is leather armor
		LPF ALTER_EFFECT
			INT_VAR
				silent = 1
				match_opcode = 145
				opcode = 177
				target = 1
				parameter1 = 0
				parameter2 = 2
				timing = 2
			STR_VAR
				resource = C0ARCAS2
		END
      END
    END
  END
BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS_IN_GAME CLABBA04.2DA BEGIN
COPY_EXISTING CLABBA04.2DA OVERRIDE
APPEND ~CLABBA04.2DA~ ~ARMORED     AP_C0SKA00  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****~
END

///////////////////////////////////////////////////////////////////////////
// Update Kit Descriptions                                               //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH description IN 
    clastext  
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~%description%.2da~ BEGIN
      COPY_EXISTING ~%description%.2DA~ ~override~
      COUNT_2DA_COLS col_count
      COUNT_2DA_ROWS col_count rows
      FOR (index = 0; index < rows; ++index) BEGIN
        READ_2DA_ENTRY index 0 col_count row_name
//bard
        PATCH_IF "%row_name%" STRING_EQUAL_CASE "BARD" BEGIN
          READ_2DA_ENTRY index 4 col_count bard_desc
          SPRINT old_bard ~– May not wear armor heavier than chain mail. (Spells cannot be cast while wearing armor.)~
          SPRINT new_bard ~– May not wear armor heavier than chain mail. (Spells may be cast while wearing armor.)~
          GET_STRREF %bard_desc% bard_text
          INNER_PATCH_SAVE bard_text ~%bard_text%~ BEGIN
            REPLACE_TEXTUALLY ~%old_bard%~ ~%new_bard%~
          END
          INNER_ACTION BEGIN
            STRING_SET_EVALUATE ~%bard_desc%~ ~%bard_text%~
          END
		END
//SKALD
        PATCH_IF "%row_name%" STRING_EQUAL_CASE "SKALD" BEGIN
          READ_2DA_ENTRY index 4 col_count skald_desc
          SPRINT old_skald ~– May Specialize (two slots) in any fighting style.~
          SPRINT new_skald ~– May Specialize (two slots) in any fighting style.
– Battle Caster: The Skald may cast spells while wearing any armor.~
          GET_STRREF %skald_desc% skald_text
          INNER_PATCH_SAVE skald_text ~%skald_text%~ BEGIN
            REPLACE_TEXTUALLY ~%old_skald%~ ~%new_skald%~
          END
          INNER_ACTION BEGIN
            STRING_SET_EVALUATE ~%skald_desc%~ ~%skald_text%~
          END
		END
		
      END
      BUT_ONLY
    END
  END
  